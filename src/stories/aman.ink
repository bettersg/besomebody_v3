/*
For cut-and-paste ease
#knot:nadia_1_intro
#ui:narrator
#background:aman_school.jpg
#music:aman_intro.mp3

#knot:aman_2_haircut_no
#ui:scene
#background:aman_school.jpg
#left_character_image:zhihao_smiling.png -> can be left blank
#left_character_name:Zhihao -> can be left blank
#right_character_image:eric_angry.png -> can be left blank
#right_character_name:Eric  -> can be left blank

#knot:aman_2_haircut_no
#ui:whatsapp
#background:whatsapp.png
#chat_group_title:Gavin Wong
#chat_group_image:gavin_profile.jpg
#speaker1_name:Gavin
#speaker2_name:Eric -> can be left blank
#timestamp:21.34

#knot:aman_2_haircut_no
#ui:email
#to:Jeffrey Tan <jeffrey@hotmail.com>
#subject:Job Application for Ravi
*/

// DECLARE GLOBAL VARIABLES
VAR aman_mood = 10

-> aman_1_intro

// CHARACTER INTRODUCTION


=== aman_1_intro ===
#knot:aman_1_intro
#ui:narrator
#background:aman_1_intro.jpg
#music:aman_intro.mp3
Aman Singh #Title
Sat Sri Akaal! I am Aman Singh. 
As a Sikh, I keep my turban and beard, and wear the steel bangle, or kara. 
Those are usually the first things people notice about me. But there’s a lot more to me than that.
I’ve recently completed my diploma in accountancy, and NS is just around the corner. That’s going to be ‘fun’...
I’m also interested in media, and I’ve been told I’m good looking. I don’t mind exploring acting and modelling jobs.
My parents and my older sister Prabhjot (we call her Jothi) have always been important in my life.
They’ve been nicer to me than usual, lately. I think it’s because I’m about to enlist. And after NS, I plan to pursue an accountancy degree in Melbourne. 
They sometimes joke about losing the baby in the family. I think they genuinely feel a little sad.
I’ll miss them, and my girlfriend Selinder. Sel and I met in poly, and people tease us because we're inseperable. It helps that she’s really, really, pretty.
I should mention one more thing. One of my best friends is Zhihao -- we’ve lived in neighbouring HDB blocks since young, so we grew up together in a way.
Zhihao’s great! But he can also be... a lot, sometimes. You’ll see.
So that’s me -- Aman. 
And this is what it’s like, to be me.

-> aman_1_1_narration 

// START STORY


=== aman_1_1_narration ===
#knot:aman_1_1_narration
#ui:narrator
#background:aman_sikh_event_hall.jpg
Zhi Hao and I are at a Sikh community event early on a Saturday morning.
-> aman_1_1


=== aman_1_1 ===
#knot:aman_1_1
#ui:scene
#background:aman_sikh_event_hall.jpg
#left_character_image:zhihao_smiling.png
#left_character_name:Zhihao
Oi! Focus la. You’re double-packing the fruits. #speaker_left
Crap. Thanks. #speaker_self
You okay? You’ve been so quiet this morning. PMS ah? #speaker_left
* [I'm just nervous] Stop it lah. I'm just nervous about enlistment. When are you going in?
    Two months more. You? #speaker_left
    Next month. Quite nervous sia. #speaker_self
    Why ah? Oh - because you posted army right? #speaker_left
* Haha must be lah[.] Anyway, thanks for doing this ya. I know volunteering is not the most exciting way to spend your weekend.
    It’s ok, good karma. Anyway, I know you’re enlisting soon, so can’t hang out already. Next month going in already right? #speaker_left
    Uh - yeah. #speaker_self
    You sound like you got handed death sentence. Why? Because going army right? #speaker_left
- Ya. I thought I would get police. All my cousins got police. Dunno why I kena army. I heard BMT training is from morning to night. How to find time to wash my hair, dry my turban cloth... #speaker_self
You keeping your turban? Die la. The cloth is never-ending. Can reach JB! #speaker_left
* [Oi, not funny] You gotta stop those ‘jokes’ man. Just cos I’m your friend and I give you a free pass, all the time...
* Funny ... ha-ha[.] Some of us have real problems on our hands you know. 
- I don’t get it though. If you’re worried about your turban, just cut hair lah. #speaker_left
* I can’t do that[.] That’s insane. And my parents would kill me!
* So actually, I’ve been thinking about it[.] It would be easier to blend in. But, my parents would kill me...
- You want to kena tekan by your parents, or kena tekan by the sergeant major? #speaker_left
Is tekan by nobody an option? #speaker_self
This is NS, sure tekan. Anyway, some of your cousins also don’t keep their turban what.  #speaker_left
Haha I remember your little cousin from the next block - remember we used to tease him in the playground? #speaker_left
Oh my god, yes! We would hide his water bottle, and after playing police and thief, we would all be so thirsty drinking our water, and he would have none. #speaker_self
Then when he asked us for a sip, we would say cannnnnnnnn... #speaker_left
And hand him our empty water bottles! Once you filled his bottle with sand, remember? #speaker_self
Eh it was your idea! We were damn mean sia. #speaker_left
You’re still mean what. #speaker_self
Anyway that kid cut hair since secondary school what. So no problem la. #speaker_left
His parents are different. Anyway, focus la! We still have to deliver all of these bags to the rental block. #speaker_self
Ok, but you go and think about it la. I really think you should just cut. Don’t make things harder for yourself.  #speaker_left

-> aman_1_2_narration


=== aman_1_2_narration ===
#knot:aman_1_2_narration
#ui:narrator
#background:aman_mirror.jpg
Later that night...

-> aman_1_2


=== aman_1_2 ===
#knot:aman_1_2
#ui:scene
#background:aman_bedroom.jpg
#right_character_image:jothi_casual.png
#right_character_name:Jothi


Oh my god, can you like, KNOCK? #speaker_self
If you need privacy, lock the door. Anyway, what are you doing? Examining your forehead lines? #speaker_right
Told you to do your yoga face exercises before bed. It will keep you youthful and glowing, like moi. #speaker_right #right_character_image:jothi_laughing.png
What do you want? #speaker_self
Can I borrow your airpods? I sent mine for servicing. #speaker_right #right_character_image:jothi_casual.png
Ya, it’s in the second drawer. Take and get out. #speaker_self
#background:aman_drawer.jpg
What is this? #speaker_right #right_character_image:jothi_concerned.png
Nothing -- #speaker_self
Don’t lie to me, what is this? #speaker_right
Just take the airpods and go please, I have a lot of stuff to do -- #speaker_self
You’ve been cutting your hair, haven’t you? #speaker_right
* [Yes, so what?] Yes, and I might cut it all off. So?
Are you KIDDING me? Papa will kick you out. #speaker_right
* None of your business[.] My life, my choices.
You better not do anything stupid. Papa will kick you out. #speaker_right
- Let him. I’m going to enlist anyway. I’ll just stay in camp. #speaker_self
How could you even say that! Doesn’t this mean anything to you? How can you take this so lightly? #speaker_right
Sis, until and unless you keep a turban yourself, or even keep your hair longer than your shoulders, or have to do bloody NS, for God’s sake, you are not in a position to comment on or judge my life choices.  #speaker_self
Don’t pretend to be so perfect. Neither of us are role models. #speaker_self
Now, will you please take the airpods, and leave me alone? #speaker_self
#right_character_image:jothi_sad.png
(Sighs.) Aman, I... #speaker_right
... I ...  #speaker_right


-> aman_1_3_narration


=== aman_1_3_narration ===
#knot:aman_1_3_narration
#ui:narrator
#background:aman_bedroom.jpg
One month later.

-> aman_1_3


=== aman_1_3 ===
#knot:aman_1_3
#ui:whatsapp
#background:whatsapp.png
#chat_group_title:Selinder
#chat_group_image:selinder_profile.jpg
#speaker1_name:Selinder
#timestamp:23.34
Babe, I’m sorry, I think I’m going to do it. #speaker_self
Jaan, don’t do anything rash. #speaker_1
Think of what this means for your faith and your identity. And your family. #speaker_1
And I think you're handsome already! #speaker_1
... #speaker_1
... hello? #speaker_1
#timestamp:09.15
Hi babe. I’m so sorry about last night. I got so tired and I just passed out on my bed. #speaker_self
It’s okay. #speaker_1
I’m sorry if I came down too hard on you. #speaker_1
I know this is your decision in the end. #speaker_1
I mean, I know what I wish you would choose. #speaker_1
But I will support you, no matter what. #speaker_1
You’re the best.  #speaker_self
Awww... #speaker_self #emoji:heart.png
So - did you decide...?  #speaker_1
You know what.  #speaker_self
I wasn’t sure up till this very moment, but I’ve decided now.  #speaker_self
* I am going to cut my hair [.] I have to do this. -> aman_1_4
* I'll keep my hair[.] You were right dear. -> aman_1_9


=== aman_1_4 ===
#knot:aman_1_4
#ui:scene
#background:aman_barbershop.jpg
#right_character_image:barber.png
#right_character_name:Barber
Yes, boy?  #speaker_right
Not crowded. Thank you, Waheguru. #inner_monologue
I want to cut my hair. #speaker_self
#right_character_image:barber_surprised.png
Cut beard or cut hair?   #speaker_right
Don’t think. Just say it. Just say it.  #inner_monologue
Cut everything.  #speaker_self
Ok. Sit. #speaker_right
#background:aman_mirror_barbershop.jpg
You sure ah, boy. #speaker_right
* [No] I uh ... ummm   #speaker_self
    Well?  #speaker_right
    Sorry, can refund?	  #speaker_self	
    Refund what? Haven't pay yet boy.   #speaker_right
	Sorry. Sorry, I’ve changed my mind. Bye! #speaker_self	
    -> aman_1_9
* Yes [,]I'm sure.
What style you want? Medium, or fade, or --   #speaker_right
I’m going NS tomorrow, so just cut very short.   #speaker_self	
#background:aman_barbershop_staring.jpg
Ok.  #speaker_right
#background:aman_barbershop_floor.jpg
You want to keep? #speaker_right
Huh? #speaker_self	
Some Singh customer ask to keep their hair. I can put in plastic bag for you.  #speaker_right
Er… no, it’s okay. Thank you.  #speaker_self	

-> aman_1_5_narration

=== aman_1_5_narration ===
#knot:aman_1_5_narration
#ui:narrator
#background:aman_outside_barber.jpg
I look so different. 
But this is the new me. 
This is the start of a new chapter in my life.

-> aman_1_5 


=== aman_1_5 ===
#knot:aman_1_5
#ui:whatsapp
#background:whatsapp.png
#chat_group_title:Family
#chat_group_image:aman_family.jpg
#speaker1_name:Papa
#timestamp:09.45



=== aman_1_9 ===



























// END STORY
-> END